apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'java'

buildscript {
    ext.streamsets_api_version = '2.7.+'
    ext.streamsets_sdk_version = '2.7.+'
    ext.slf4j_version = '1.7.+'
    ext.junit_version = '4.+'
    ext.logback_version = '1.2.+'
    ext.jsonpath_version = '2.4.+'
    ext.jackson_version = '2.9.+'
    ext.version_jcommander = '1.72'
    ext.version_lombok_gradleplugin = '1.10'
    ext.version_lombok = '1.16.18'
    ext.version_jetbrainsannotations = '13.0'
    ext.version_aws_sdk = '1.11.226'
    ext.dependency_management_version = '0.6.0.RELEASE'

    repositories {
        jcenter()
        mavenLocal() // TODO: isn't this a bit dangerous, it will make a build succeed when sometimes shouldn't, also snapshot versions would be inconsistent
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.13"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
    //    classpath "io.franzbecker:gradle-lombok:$version_lombok"
        // The dependency management plugin changes Gradle's handling of a pom's exclusions so that they behave as they do in Maven.
        // https://spring.io/blog/2015/02/23/better-dependency-management-for-gradle
        classpath "io.spring.gradle:dependency-management-plugin:$dependency_management_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath 'ru.vyarus:gradle-pom-plugin:1.2.0'
    }
}

allprojects {
    group 'com.ebsco.entarch'
    version '1.0.9-SNAPSHOT'
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'net.ltgt.apt'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'distribution'
    apply plugin: 'maven'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
    apply plugin: 'ru.vyarus.pom'

    compileJava.options.encoding = 'UTF-8'
    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        systemProperty "file.encoding", "utf-8"
//        testLogging {
//            exceptionFormat "full" // default is "short"
//            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
//        }
    }

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        testCompile "junit:junit:$junit_version"

        compile group: 'org.jetbrains', name: 'annotations', version: version_jetbrainsannotations

        compileOnly "org.projectlombok:lombok:$version_lombok"
        apt "org.projectlombok:lombok:$version_lombok"
        testApt "org.projectlombok:lombok:$version_lombok"

    }

    configurations.all {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j'
    }

    tasks.withType(Tar) {
        compression = Compression.GZIP
    }
    shadowJar {
        configurations = [project.configurations.runtime]
//        destinationDir file('.')
//        include '**/*-all.jar'
    }
    project.tasks.assemble.dependsOn project.tasks.shadowJar
    artifacts {
        archives shadowJar
    }
    distributions {
        main {
            contents {
                into('lib') {
                    from shadowJar
                }
            }
        }
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact shadowJar
                artifact distTar
            }
        }
    }

}

artifactory {
    contextUrl = 'https://eis.jfrog.io/eis'
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = System.getenv('ArtifactoryUser')
            password = System.getenv('ArtifactoryPassword')
        }
        defaults { publications('mavenJava') }
    }
}
